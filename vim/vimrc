" Copy or symlink this file to ~/.vimrc

" avoiding annoying CSApprox warning message
let g:CSApprox_verbose_level = 0

set nocompatible               " be iMproved
filetype off                   " required!

" Vundle
  set rtp+=~/.vim/bundle/Vundle.vim/
  call vundle#begin()

  if filereadable(expand("~/.vimrc.bundles"))
    source ~/.vimrc.bundles
  endif

  call vundle#end()               " required
  filetype plugin indent on       " required

" Configuration
set tags=tags                     " ctags

" This makes Vim slow with Ruby and the new Vim regex engine after v7+
syntax on                         " Turn on syntax highlighting.
" This relies on the old regex
set re=1

" syntax on is pretty slow after updating VIM to v8-1600. The following
" 2 lines make it fast
"
" https://stackoverflow.com/questions/16902317/vim-slow-with-ruby-syntax-highlighting
set ttyfast
set lazyredraw

set synmaxcol=200
filetype plugin indent on         " Turn on file type detection.

runtime macros/matchit.vim        " Load the matchit plugin.

"highlight ColorColumn ctermbg=7
highlight ColorColumn guibg=darkgray ctermbg=darkgray
set colorcolumn=80
"let &colorcolumn=join(range(81,999),",")
"let &colorcolumn="80,".join(range(120,999),",")
set pastetoggle=<F2>
set showcmd                       " Display incomplete commands.
set showmode                      " Display the mode you're in.

set backspace=indent,eol,start    " Intuitive backspacing.
set cursorline

set hidden                        " Handle multiple buffers better.

set wildmenu                      " Enhanced command line completion.
set wildmode=full                 " Complete files like a shell.

set ignorecase                    " Case-insensitive searching.
set smartcase                     " But case-sensitive if expression contains a capital letter.

set number                        " Show line numbers.
set ruler                         " Show cursor position.

set incsearch                     " Highlight matches as you type.
set hlsearch                      " Highlight matches.

set wrap                          " Turn on line wrapping.
au BufNewFile,BufRead,BufReadPost *.md set linebreak
set scrolloff=3                   " Show 3 lines of context around the cursor.

set title                         " Set the terminal's title

set visualbell                    " No beeping.

set nobackup                      " Don't make a backup before overwriting a file.
set nowritebackup                 " And again.
set directory=/var/tmp            " Keep swap files in one location

" UNCOMMENT TO USE
set tabstop=2                    " Global tab width.
set shiftwidth=2                 " And again, related.
set expandtab                    " Use spaces instead of tabs

set laststatus=2                  " Show the status line all the time
" Useful status information at bottom of screen
set statusline=[%n]\ %<%.99f\ %h%w%m%r%y\ %{exists('*CapsLockStatusline')?CapsLockStatusline():''}%=%-16(\ %l,%c-%v\ %)%P
let &statusline .= '%{cfi#format(" [%s()]", "")}'

set foldmethod=indent
set foldlevelstart=99
set foldlevel=99
au BufRead,BufNewFile *.scss set filetype=scss
au BufRead,BufNewFile *.ejs set filetype=html
au BufRead,BufNewFile *.md setlocal textwidth=80

hi CursorLineNR cterm=bold
augroup CLNRSet
  autocmd! ColorScheme * hi CursorLineNR cterm=bold ctermfg=green
augroup END


" LARGE FILES
"
" file is large from 2mb
let g:LargeFile = 1024 * 1024 * 2
augroup LargeFile
  autocmd BufReadPre * let f=getfsize(expand("<afile>")) | if f > g:LargeFile || f == -2 | call LargeFile() | endif
augroup END

function! LargeFile()
  " no syntax highlighting etc
  set eventignore+=FileType
  " save memory when other file is viewed
  setlocal bufhidden=unload
  " is read-only (write with :w new_filename)
  setlocal buftype=nowrite
  " no undo possible
  setlocal undolevels=-1
  " display message
  autocmd VimEnter *  echo "The file is larger than " . (g:LargeFile / 1024 / 1024) . " MB, so some options are changed (see .vimrc for details)."
endfunction

" "Theme
colorscheme alex_ir_black
"railscasts


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" DEFAULT BEHAVIORS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Uncomment the following to have Vim jump to the last position when
" reopening a file
if has("autocmd")
  au BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$")
    \| exe "normal! g`\"" | endif
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" PLUGINS CONFIGURATIONS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Syntastic (not using currently)
let g:syntastic_mode_map = {
\ "mode": "active",
\ "passive_filetypes": ["cucumber"] }
let g:syntastic_ruby_checkers = ["mri", "rubocop", "reek"]

" ACK + AG
if executable('ag')
  let g:ackprg = 'ag --vimgrep'
endif

" ALE plugin
let g:ale_sign_error = 'E'
let g:ale_sign_warning = 'W'
let g:ale_sign_column_always = 1
let g:ale_lint_on_text_changed = 'normal' " never
let g:ale_lint_on_insert_leave = 1 " default 0
let g:ale_lint_delay = 10 " defaults to 200
let g:ale_echo_delay = 10 " defaults 10
let g:ale_completion_delay = 100 "defaults 100
let g:ale_set_highlights = 1

highlight ALEErrorSign ctermfg=red
highlight ALEWarningSign ctermfg=red

" Only use linters that make sense
function! SetAleBufferLinters()
  let ruby_linters = ["ruby"]

  if filereadable(".rubocop.yml") | :call add(ruby_linters, "rubocop") | endif
  if filereadable(".reek.yml")
    :call add(ruby_linters, "reek")
  endif
  if filereadable("rails_best_practices.yml") | :call add(ruby_linters, "rails_best_pratices") | endif

  let b:ale_linters = {
  \   'ruby': ruby_linters,
  \}
endfunction
augroup AleGroup
  autocmd!
  autocmd FileType,BufEnter * call SetAleBufferLinters()
augroup END

" git gutter plugin
let g:gitgutter_enabled = 1
let g:gitgutter_max_signs = 999  " 500 is the default value

" zencoding plugin
 let g:user_zen_settings = {
  \  'php' : {
  \    'extends' : 'html',
  \    'filters' : 'c',
  \  },
  \  'xml' : {
  \    'extends' : 'html',
  \  },
  \  'erb' : {
  \    'extends' : 'html',
  \  },
  \  'haml' : {
  \    'extends' : 'html',
  \  },
  \}

" CtrlP
set runtimepath^=~/.vim/bundle/ctrlp.vim
let g:ctrlp_custom_ignore = 'tmp$\|deps\|_build\|log\/\|vendor/assets\|vendor/bundle\|vendor/plugins\|.jpg\|.png\|.jpeg\|.gif\|spec/vcr\|public/uploads\|public/themes/cloud\|spec/cassett\|\.git\|cassett\|/.git\|coverage\|.DS_STORE\|node_modules\|test_build\|bower_'
let g:ctrlp_root_markers = ['ctrlp_root']
let g:ctrlp_switch_buffer = 'et'
let g:ctrlp_cache_dir = $HOME . '/.cache/ctrlp'
let g:ctrlp_clear_cache_on_exit = 1
set wildignore+=*/tmp/*,*.so,*.swp,*.zip,*/vendor/*,*/\.git/*
set wildignorecase

" https://github.com/JazzCore/ctrlp-cmatcher
" let g:ctrlp_match_func = {'match' : 'matcher#cmatch' }

let g:ctrlp_map = '<c-p>'
"let g:ctrlp_user_command = 'find %s -type f'
if executable('ag')
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
endif

" Paste.vim
let g:paste_no_mappings = 1
" nnoremap <shortcut> :Paste<CR>

function! SetCtrlPPathTheSameAsVim()
  let new_dir = expand('%:h')
  exec ':cd ' . new_dir
  let g:ctrlp_working_path_mode=''
  exec ':echo "Changed CtrlP and Vim path to current folder."'
endfunction

" When you go to another project inside of a Rails project (e.g Ember project
" in app/assets/javascript/my_project), CtrlP still uses ./ as root. By typing
" this command, CtrlP will :cd into this internal project.
command! CtrlPVimPath call SetCtrlPPathTheSameAsVim()
map <leader>p :call SetCtrlPPathTheSameAsVim()<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CUSTOM SHORTCUTS & KEY BINDINGS
"
" For tests & specs custom functions and key bindings, look at the respective
" section below
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  set timeoutlen=500
  let mapleader = ","

  " save by pressing ,s instead of :w
  map <Leader>s <ESC>:w<CR>
  map <Leader>be :BufExplorer<CR>

  " Prevents Vim from scrolling when splitting the window horizontally.
    nnoremap <C-W>s Hmx`` \|:split<CR>`xzt``

  " ]p to paste and indent code automatically
    nnoremap ]p p=`]

  " Motion for "next object". For example, "din(" would go to the next "()" pair
  " and delete its contents.
    onoremap an :<c-u>call <SID>NextTextObject('a')<cr>
    xnoremap an :<c-u>call <SID>NextTextObject('a')<cr>
    onoremap in :<c-u>call <SID>NextTextObject('i')<cr>
    xnoremap in :<c-u>call <SID>NextTextObject('i')<cr>

    function! s:NextTextObject(motion)
      echo
      let c = nr2char(getchar())
      exe "normal! f".c."v".a:motion.c
    endfunction

  " Folding
    nnoremap <leader>1 :set foldlevel=1<cr>
    nnoremap <leader>2 :set foldlevel=2<cr>
    nnoremap <leader>3 :set foldlevel=3<cr>
    nnoremap <leader>4 :set foldlevel=4<cr>
    nnoremap <leader>5 :set foldlevel=5<cr>
    nnoremap <leader>6 :set foldlevel=6<cr>
    nnoremap <leader>7 :set foldlevel=7<cr>
    nnoremap <leader>8 :set foldlevel=8<cr>
    nnoremap <leader>9 :set foldlevel=9<cr>
    nnoremap <leader>0 :set foldlevel=99<cr>

  " Tab mappings.
    map <leader>tt :tabnew<cr>
    map <leader>ts :tab split<cr>
    map <leader>te :tabedit
    map <leader>tc :tabclose<cr>
    map <leader>to :tabonly<cr>
    map <leader>tn :tabnext<cr>
    map <leader>tp :tabprevious<cr>
    map <leader>tf :tabfirst<cr>
    map <leader>tl :tablast<cr>
    map <leader>tm :tabmove

  " Enter clears search highlight (replaced with running tests)
  " nmap <CR> :nohlsearch<CR>

  " When pasting in visual mode, doesn't replace current register
  " vnoremap p "_dP
  " I haven't found how to hide this function (yet)
  function! RestoreRegister()
    let @" = s:restore_reg
    return ''
  endfunction

  function! s:Repl()
      let s:restore_reg = @"
      return "p@=RestoreRegister()\<cr>"
  endfunction

  " this supports "rp that replaces the selection by the contents of @r
  " I was using vnoremap but that was getting in select as well and
  " conflicting with Snipmate.
  xnoremap <silent> <expr> p <sid>Repl()

  " Goes to the end of the line (pressing shift+4 is annoying)
  nmap 44 $
  vmap 44 $h

  function! SearchCurrentPath()
    let l:word = expand("<cword>")
    call feedkeys(":CtrlP\<CR>".l:word, "t")
  endfunction
  nnoremap <leader>g :call SearchCurrentPath()<cr>

  " Tab becomes Esc
  " inoremap <m-i> <tab>
  " nnoremap <tab> <esc>
  " vnoremap <tab> <esc>gV
  " inoremap <tab> <esc>`^

  " Updates CTags
  map <F5> :exec "!ctags -R --exclude=.git --exclude=vendor/bundle"<CR>

  function! PropheticTag()
    if v:count1 == "1"
      :echo "Not prophetic"
      return ""
    endif

    execute "normal! " . v:count1 . "G"
    execute ":tag '" . expand("<cword>") . "'"
    return ""
  endfunction

  "nnoremap <C-[> :<C-U>call PropheticTag()<CR>


  nmap <leader>vim :e ~/.vimrc<CR>

  " Buffer
  "
  " :Ls will mimic :ls, but order by filenames, not number
  command! -bang Ls redir @" | silent ls<bang> | redir END | echo " " |
   \ perl {
   \ my $msg=VIM::Eval('@"');
   \ my %list=();
   \ my $key, $value;
   \ while($msg =~ m/(.*?line\s+\d+)/g)
   \ {
   \ $value = $1;
   \ $value =~ m/"([^"]+)"/;
   \ $key = $1;
   \ ($^O =~ /mswin/i) and $key = lc($key);
   \ $list{$key} = $value;
   \ }
   \ my $msg = '';
   \ for $key (sort keys %list)
   \ {
   \ $msg .= "$list{$key}\n";
   \ }
   \ VIM::Msg($msg);
   \ }
   \ <CR>

  " opens buffer list and writes ':b ', so you just have to type a number
  if has('perl')
    nnoremap <leader>l :Ls<CR>:b<Space>
  else
    nnoremap <leader>l :ls<CR>:b<Space>
  endif

  " Spacebar saves
  "
  " If current file is .vimrc, it will then reload it to refresh the changes
  nnoremap <Space> :exec ":write" \| if match(expand('%'), ".vim") >= 1 \| exec ":so %" \| endif<cr>

  " Resizing windows
  nnoremap <C-h> :vertical resize +5<cr>
  nnoremap <C-l> :vertical resize -5<cr>
  nnoremap <C-k> :resize +5<cr>
  nnoremap <C-j> :resize -5<cr>

  inoremap  <Up>     <NOP>
  inoremap  <Down>   <NOP>
  inoremap  <Left>   <NOP>
  inoremap  <Right>  <NOP>
  noremap   <Up>     <NOP>
  noremap   <Down>   <NOP>
  noremap   <Left>   <NOP>
  noremap   <Right>  <NOP>

  " When you've writen : (colon), press %% very fast and it'll write the current
  " file's directory
  cnoremap %% <C-R>=expand('%:h').'/'<cr>
  map <leader>e :edit %%
  map <leader>v :view %%

  " RUBY & RAILS
    " Keeps current file in on side, and its test file in the other
    " (depends on Rails.vim)
    map <leader>A :on<CR>\|:vsplit<CR>\|<c-w>l\|:A<CR>

    " :A is from rails.vim plugin
    nnoremap <Leader>a :A<CR>

    " deletes the remaining chars from a string
    nnoremap <Leader>ds vf"hd

    map <leader>ga :CtrlPClearCache<cr>\|:CtrlP app/assets<cr>
    map <leader>gv :CtrlPClearCache<cr>\|:CtrlP app/views<cr>
    map <leader>gc :CtrlPClearCache<cr>\|:CtrlP app/controllers<cr>
    map <leader>gm :CtrlPClearCache<cr>\|:CtrlP app/models<cr>
    map <leader>gl :CtrlPClearCache<cr>\|:CtrlP lib<cr>
    map <leader>gs :CtrlPClearCache<cr>\|:CtrlP spec<cr>
    map <leader>gu :CtrlPClearCache<cr>\|:CtrlP spec/unit<cr>
    map <leader>f :CtrlPClearCache<cr>\|:CtrlP<cr>
    " map <leader>f :CtrlPClearCache<cr>\|:CtrlP<cr>
    map <leader>f :CtrlP<cr>
    nmap <leader>b :CtrlPBuffer<cr>
    map <leader>F :CtrlPClearCache<cr>\|:CtrlP<cr>

    let g:splitjoin_trailing_comma = 0
    let g:splitjoin_ruby_hanging_args = 0
    let g:splitjoin_ruby_curly_braces = 0
    nmap sj :SplitjoinSplit<cr>
    nmap sk :SplitjoinJoin<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" CUSTOM FUNCTIONS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  " Rename current file
  function! RenameFile()
    let old_name = expand('%')
    let old_directory = $PWD . "/" . fnamemodify(expand(old_name), ':h')
    let new_name = input('Renaming file. New file name: ', expand('%'), 'file')
    let new_directory = fnamemodify(expand(new_name), ':h')
    if new_name != '' && new_name != old_name
      exec ':silent !mkdir -p ' . new_directory
      exec ':saveas ' . new_name
      exec ':silent !rm ' . old_name
      exec ':silent !find ' . old_directory . ' -type d -empty -exec rmdir {} +'
      redraw!
    endif
    exec ':CtrlPClearCache'
  endfunction

  " Copy current file to a new location
  function! CopyFile()
    let old_name = expand('%')
    let new_name = input('Copying file. New file name: ', expand('%'), 'file')
    let new_directory = fnamemodify(expand(new_name), ':h')
    if new_name != '' && new_name != old_name
      exec ':silent !mkdir -p ' . new_directory
      exec ':saveas ' . new_name
      redraw!
    endif
    exec ':CtrlPClearCache'
  endfunction

  map <leader>n :call RenameFile()<cr>
  map <leader>c :call CopyFile()<cr>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" TESTS & SPECS FUNCTIONS
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
  function! SleepForZeusToReload()
    exec ':sleep 250m'
  endfunction

  nnoremap <cr> <ESC>:w<CR>\|:call SleepForZeusToReload()\|:call RunTestFile()<cr>
  nnoremap <Bslash> <ESC>:w<CR>\|:sleep 50m<CR>\|:call RunNearestTest()<cr>
  map <leader>t :call RunTestFile()<cr>
  map <leader>r :call RunNearestTest()<cr>
  map <leader>bt <ESC>:w<CR>\|:!bundle exec rspec --color --no-drb %<cr>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" ENVIRONMENT CONFIGURATIONS
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
  " taken from http://vim.wikia.com/wiki/Highlight_unwanted_spaces
  highlight RedundantSpaces ctermbg=red guibg=red
  match RedundantSpaces /\s\+$/
  autocmd BufWinEnter * match RedundantSpaces /\s\+$/
  autocmd InsertEnter * match RedundantSpaces /\s\+\%#\@<!$/
  autocmd InsertLeave * match RedundantSpaces /\s\+$/
  autocmd BufWinLeave * call clearmatches()

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" INDENTING WHEN PASTING
" Paste and indent if we're at the beginning of a line. Else, just paste.
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! PasteAndIndent()
  let col = col('.') - 1
  if !col || getline('.')[col - 1] !~ '\k'
    "execute ":normal! p=`]"
    execute ":echo 1111"
    execute ":normal! p"
  else
    execute ":echo 2222"
    execute ":normal! p"
  endif
endfunction

" nnoremap p :call PasteAndIndent()<cr>

" ]p to paste and indent code automatically
" nnoremap ]p p=`]

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" MULTIPURPOSE TAB KEY
" Indent if we're at the beginning of a line. Else, do completion.
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! InsertTabWrapper()
    let col = col('.') - 1
    if !col || getline('.')[col - 1] !~ '\k'
        return "\<tab>"
    else
        return "\<c-p>"
    endif
endfunction
" imap <tab> <c-r>=InsertTabWrapper()<cr>
inoremap <s-tab> <c-n>


" PLUGINS
"
" Nerdtree
map <C-n> :NERDTreeToggle<CR>
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
